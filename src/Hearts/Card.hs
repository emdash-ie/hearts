{-# LANGUAGE DeriveGeneric #-}
{-# LANGUAGE LambdaCase #-}
{-# LANGUAGE NamedFieldPuns #-}
{-# LANGUAGE OverloadedStrings #-}

module Hearts.Card (
  Card (..),
  Suit (..),
  Value (..),
  score,
  allCards,
  sortCards,
  cardHtml,
  faceDownCard,
) where

import qualified Data.Aeson as Aeson
import Data.List (sortOn)
import Data.Monoid (Sum)
import Data.Text (Text)
import qualified Data.Text as Text
import GHC.Generics (Generic)
import Lucid (HtmlT, ToHtml (..), class_, span_)
import Servant (FromHttpApiData, ToHttpApiData, toUrlPiece)
import Servant.API (parseUrlPiece)

data Card = Card {suit :: Suit, value :: Value}
  deriving (Eq, Generic)

instance Show Card where
  show = \case
    Card Clubs Two -> "ðŸƒ’"
    Card Clubs Three -> "ðŸƒ“"
    Card Clubs Four -> "ðŸƒ”"
    Card Clubs Five -> "ðŸƒ•"
    Card Clubs Six -> "ðŸƒ–"
    Card Clubs Seven -> "ðŸƒ—"
    Card Clubs Eight -> "ðŸƒ˜"
    Card Clubs Nine -> "ðŸƒ™"
    Card Clubs Ten -> "ðŸƒš"
    Card Clubs Jack -> "ðŸƒ›"
    Card Clubs Queen -> "ðŸƒ"
    Card Clubs King -> "ðŸƒž"
    Card Clubs Ace -> "ðŸƒ‘"
    Card Diamonds Two -> "ðŸƒ‚"
    Card Diamonds Three -> "ðŸƒƒ"
    Card Diamonds Four -> "ðŸƒ„"
    Card Diamonds Five -> "ðŸƒ…"
    Card Diamonds Six -> "ðŸƒ†"
    Card Diamonds Seven -> "ðŸƒ‡"
    Card Diamonds Eight -> "ðŸƒˆ"
    Card Diamonds Nine -> "ðŸƒ‰"
    Card Diamonds Ten -> "ðŸƒŠ"
    Card Diamonds Jack -> "ðŸƒ‹"
    Card Diamonds Queen -> "ðŸƒ"
    Card Diamonds King -> "ðŸƒŽ"
    Card Diamonds Ace -> "ðŸƒ"
    Card Spades Two -> "ðŸ‚¢"
    Card Spades Three -> "ðŸ‚£"
    Card Spades Four -> "ðŸ‚¤"
    Card Spades Five -> "ðŸ‚¥"
    Card Spades Six -> "ðŸ‚¦"
    Card Spades Seven -> "ðŸ‚§"
    Card Spades Eight -> "ðŸ‚¨"
    Card Spades Nine -> "ðŸ‚©"
    Card Spades Ten -> "ðŸ‚ª"
    Card Spades Jack -> "ðŸ‚«"
    Card Spades Queen -> "ðŸ‚­"
    Card Spades King -> "ðŸ‚®"
    Card Spades Ace -> "ðŸ‚¡"
    Card Hearts Two -> "ðŸ‚²"
    Card Hearts Three -> "ðŸ‚³"
    Card Hearts Four -> "ðŸ‚´"
    Card Hearts Five -> "ðŸ‚µ"
    Card Hearts Six -> "ðŸ‚¶"
    Card Hearts Seven -> "ðŸ‚·"
    Card Hearts Eight -> "ðŸ‚¸"
    Card Hearts Nine -> "ðŸ‚¹"
    Card Hearts Ten -> "ðŸ‚º"
    Card Hearts Jack -> "ðŸ‚»"
    Card Hearts Queen -> "ðŸ‚½"
    Card Hearts King -> "ðŸ‚¾"
    Card Hearts Ace -> "ðŸ‚±"

instance Aeson.ToJSON Card
instance Aeson.FromJSON Card

instance ToHtml Card where
  toHtml c@(Card s _) =
    let cardClass =
          case s of
            Clubs -> "clubs"
            Spades -> "spades"
            Diamonds -> "diamonds"
            Hearts -> "hearts"
     in cardHtml (Just cardClass) (Text.pack (show c))
  toHtmlRaw = toHtml

faceDownCard :: Text
faceDownCard = "ðŸ‚ "

cardHtml :: Monad m => Maybe Text -> Text -> HtmlT m ()
cardHtml extraClass c =
  span_ [class_ ("card" <> maybe "" (" " <>) extraClass)] (toHtml c)

instance FromHttpApiData Card where
  parseUrlPiece p =
    let (suitText, valueText) = Text.splitAt 1 p
        s = case suitText of
          "C" -> Right Clubs
          "D" -> Right Diamonds
          "S" -> Right Spades
          "H" -> Right Hearts
          _ -> Left "Unrecognised suit"
        v = case valueText of
          "2" -> Right Two
          "3" -> Right Three
          "4" -> Right Four
          "5" -> Right Five
          "6" -> Right Six
          "7" -> Right Seven
          "8" -> Right Eight
          "9" -> Right Nine
          "10" -> Right Ten
          "J" -> Right Jack
          "Q" -> Right Queen
          "K" -> Right King
          "A" -> Right Ace
          _ -> Left "Unrecognised value"
     in Card <$> s <*> v

instance ToHttpApiData Card where
  toUrlPiece Card{suit, value} =
    let suitText = case suit of
          Clubs -> "C"
          Diamonds -> "D"
          Spades -> "S"
          Hearts -> "H"
        valueText = case value of
          Two -> "2"
          Three -> "3"
          Four -> "4"
          Five -> "5"
          Six -> "6"
          Seven -> "7"
          Eight -> "8"
          Nine -> "9"
          Ten -> "10"
          Jack -> "J"
          Queen -> "Q"
          King -> "K"
          Ace -> "A"
     in suitText <> valueText

data Suit
  = Clubs
  | Diamonds
  | Spades
  | Hearts
  deriving (Eq, Bounded, Enum, Ord, Generic)

instance Show Suit where
  show = \case
    Clubs -> "â™£"
    Diamonds -> "â™¦"
    Spades -> "â™ "
    Hearts -> "â™¥"

instance Aeson.ToJSON Suit
instance Aeson.FromJSON Suit

data Value
  = Two
  | Three
  | Four
  | Five
  | Six
  | Seven
  | Eight
  | Nine
  | Ten
  | Jack
  | Queen
  | King
  | Ace
  deriving (Eq, Bounded, Enum, Ord, Generic)

instance Show Value where
  show = \case
    Two -> "2"
    Three -> "3"
    Four -> "4"
    Five -> "5"
    Six -> "6"
    Seven -> "7"
    Eight -> "8"
    Nine -> "9"
    Ten -> "10"
    Jack -> "J"
    Queen -> "Q"
    King -> "K"
    Ace -> "A"

instance Aeson.ToJSON Value
instance Aeson.FromJSON Value

-- | The score taken for taking this card in a trick.
score :: Card -> Sum Integer
score = \case
  Card Spades Queen -> 13
  Card Hearts _ -> 1
  _ -> 0

allCards :: [Card]
allCards =
  Card
    <$> [minBound .. maxBound]
    <*> [minBound .. maxBound]

sortCards :: [Card] -> [Card]
sortCards = sortOn suit . sortOn value
